
rule all_analyse_dalu_bat:
    input:
        os.path.join(outdir, "dalu_bat/filters/asv_table_final.tsv"),


rule filter_indel_codon_stop_pooling_bat:
    input:
        os.path.join(outdir, "dalu_bat/filters/asv_table_pooled_replicates.tsv"),
        os.path.join(outdir, "vtam_bat/all_asv_vtam_bat.tsv"),
        "metafiles/sample_types_bat.tsv",
        "metafiles/mock_composition_bat.tsv",
    output:
        os.path.join(outdir, "dalu_bat/filters/asv_table_final.tsv"),
    params:
        min_readcount=min_readcount,
    shell:
        """perl scripts/add_filters.pl -asv {input[0]} -out_asv {output[0]} -id_col 0 -first_sample_col 3 -motif_after_last_sample sequence -seq_col '-1' -all_seqs_vtam {input[1]} -sample_types {input[2]} -mock_compositon {input[3]} -match_seqid_to_vtam 1 -filter_indel 1 -filter_codon_stop 1 -min_readcount {params.min_readcount}"""

rule sqlite_variants_bat:
    input:
        os.path.join(outdir, "vtam_bat/vtam_bat_final_taxa.tsv"),
    output:
        os.path.join(outdir, "vtam_bat/all_asv_vtam_bat.tsv"),
    params:
        os.path.join(outdir, "vtam_bat/db_bat.sqlite"),
    shell:
        """sqlite3 -separator $'\t' {params[0]} 'select distinct Variant.id, Variant.sequence from Variant' >{output[0]}"""

rule replicate_pooling_bat:
    input:
        os.path.join(outdir, "dalu_bat/dada/asv.fasta"),
        os.path.join(outdir, "dalu_bat/dada/asv_table_lulu.csv"),
        "metafiles/sample_types_bat.tsv",
    output:
        os.path.join(outdir, "dalu_bat/filters/asv_table_pooled_replicates.tsv"),
    shell:
        """perl scripts/pool_replicates.pl -asvseq {input[0]} -asvtable {input[1]} -out {output[0]} -septable ',' -samples {input[2]} -marker COI"""

rule lulu_bat:
    input:
        os.path.join(outdir, "dalu_bat/dada/asv_table_names.csv"),
        os.path.join(outdir, "dalu_bat/dada/asv_auto_blast.tsv"),
    output:
        os.path.join(outdir, "dalu_bat/dada/asv_table_lulu.csv"),
    shell:
        """Rscript scripts/lulu.R {input[0]} {input[1]} {output[0]}"""

rule blastn_bat:
    input:
        os.path.join(outdir, "dalu_bat/dada/asv.fasta"),
        os.path.join(outdir, "dalu_bat/dada/asv.fasta.nsq"),
    output:
        os.path.join(outdir, "dalu_bat/dada/asv_auto_blast.tsv"),
    shell:
        """blastn -db {input[0]} -outfmt '6 qseqid sseqid pident' -out {output[0]} -qcov_hsp_perc 80 -perc_identity 80 -query {input[0]}"""

rule makeblastdb_bat:
    input:
        os.path.join(outdir, "dalu_bat/dada/asv.fasta"),
    output:
        os.path.join(outdir, "dalu_bat/dada/asv.fasta.nsq"),
    params:
        os.path.join(outdir, "dalu_bat/dada/asv.fasta"),
    shell:
        """makeblastdb -in {input[0]} -parse_seqids -dbtype nucl -out {params[0]}"""


rule dada_bat:
    input:
        os.path.join(outdir, "dalu_bat/fastq/COI-T_F-3_rv.fastq.gz"),
    output:
        os.path.join(outdir, "dalu_bat/dada/track.csv"),
        os.path.join(outdir, "dalu_bat/dada/asv.fasta"),
        os.path.join(outdir, "dalu_bat/dada/asv_table_names.csv"),
    params:
        truncLen0=120,
        seq_length_range0=124,
        seq_length_range1=163,
        dataset="bat",
    shell:
         """Rscript scripts/dada.R $(dirname {input[0]}) {output[0]} {output[1]} {output[2]} {params.truncLen0} {params.seq_length_range0} {params.seq_length_range1} {params.dataset}"""


